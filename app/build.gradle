apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-parcelize'

android {
    compileSdkVersion 33
    def name = "1.4.3"
    def code = 40

    defaultConfig {
        applicationId "com.saulhdev.neofeed"
        minSdkVersion 24
        targetSdkVersion 32
        versionName name
        versionCode code
    }

    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            output.outputFileName = "Neo_Feed_v" + variant.versionName + "_Build_" + variant.versionCode + ".apk"
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            applicationIdSuffix ".dev"
            signingConfig signingConfigs.debug
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main {
            java.srcDirs = ['src/main/java', 'src/main/kotlin']
            assets.srcDirs = ['src/main/assets']
            res.srcDirs = ['src/main/res']
        }
    }

    signingConfigs {
        debug {
            storeFile file("debug.keystore")
            storePassword "android"
            keyAlias "androiddebugkey"
            keyPassword "android"
        }
    }

    kotlinOptions {
        jvmTarget = '11'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    buildFeatures {
        compose = true
        viewBinding true
    }

    composeOptions {
        kotlinCompilerExtensionVersion = "1.2.0"
    }
    packagingOptions {
        // Rome incorrectly bundles stuff in its jar
        pickFirst "rome-utils-1.18.0.jar"
    }

    lintOptions {
        disable 'MissingTranslation'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'androidx.appcompat:appcompat:1.5.1'
    implementation 'androidx.appcompat:appcompat-resources:1.5.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.core:core-ktx:1.9.0'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.5.2'
    implementation 'androidx.navigation:navigation-ui-ktx:2.5.2'
    implementation 'androidx.navigation:navigation-runtime-ktx:2.5.2'
    implementation 'androidx.preference:preference-ktx:1.2.0'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'

    //Compose
    implementation 'androidx.compose.ui:ui:1.2.1'
    implementation 'androidx.compose.ui:ui-tooling:1.2.1'
    implementation 'androidx.compose.ui:ui-tooling-preview:1.2.1'
    implementation 'androidx.compose.animation:animation:1.2.1'
    implementation 'androidx.compose.material3:material3:1.0.0-beta02'
    implementation 'androidx.compose.material:material:1.2.1'
    implementation 'androidx.navigation:navigation-compose:2.6.0-alpha01'

    //Room
    implementation 'androidx.room:room-ktx:2.4.3'
    implementation 'androidx.room:room-paging:2.4.3'
    kapt 'androidx.room:room-compiler:2.4.3'

    //DataStore
    implementation 'androidx.datastore:datastore-preferences:1.0.0'

    //Material
    implementation 'com.google.android.material:material:1.8.0-alpha01'
    implementation 'com.google.android.material:compose-theme-adapter-3:1.0.18'

    //Accompanist
    implementation("com.google.accompanist:accompanist-navigation-animation:0.25.0")
    implementation "com.google.accompanist:accompanist-webview:0.26.3-beta"

    implementation 'io.coil-kt:coil:2.2.1'
    implementation 'io.coil-kt:coil-compose:2.2.1'
    implementation 'com.prof18.rssparser:rssparser:4.0.2'
    implementation 'io.github.fornewid:material-motion-compose-core:0.8.4'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.6.4'
    implementation 'com.squareup.okhttp3:logging-interceptor:5.0.0-alpha.10'

    implementation "com.jakewharton.threetenabp:threetenabp:1.2.4"
    implementation "org.jsoup:jsoup:1.14.2"
    implementation "org.ccil.cowan.tagsoup:tagsoup:1.2.1"

    implementation project(':homefeeder-sdk')
    // RSS
    implementation("com.rometools:rome:1.18.0") {
        exclude group: 'com.rometools', module: 'rome-utils'
    }
    implementation "com.rometools:rome-modules:1.18.0"
    implementation project(':jsonfeed-parser')
}
